DEFINE_ACTION_FUNCTION extend_area_script 
    STR_VAR area=""
            top=""
            bottom=""
BEGIN
    // find the area script from the area file
    COPY_EXISTING "%area%.are" override
           READ_ASCII 0x94 script
    BUT_ONLY
    // if it doesn't exist, give it a default name
    ACTION_IF "%script%" STRING_EQUAL "" BEGIN
          OUTER_INNER_PATCH_SAVE script "%area%" BEGIN
                REPLACE_TEXTUALLY "FW" "_AR" // TUTU naming conventions
          END
    END
    // extend the script
    ACTION_IF "%top%" STRING_COMPARE "" BEGIN
        EXTEND_TOP "%script%.bcs" "%top%" EVALUATE_BUFFER
    END
    ACTION_IF "%bottom%" STRING_COMPARE "" BEGIN
       EXTEND_BOTTOM "%script%.bcs" "%bottom%" EVALUATE_BUFFER
    END
END 

DEFINE_ACTION_FUNCTION replacetext_area_script 
    STR_VAR area=""
            oldtext=""
            newtext=""
BEGIN
    // find the area script from the area file
    COPY_EXISTING "%area%.are" override
           READ_ASCII 0x94 script
    BUT_ONLY
    // if it doesn't exist, give it a default name
    ACTION_IF "%script%" STRING_EQUAL "" BEGIN
          OUTER_INNER_PATCH_SAVE script "%area%" BEGIN
                REPLACE_TEXTUALLY "FW" "_AR" // TUTU naming conventions
          END
    END
    // replace script text
    ACTION_IF "%oldtext%" STRING_COMPARE "" BEGIN
    	COPY_EXISTING "%script%.bcs" override
    		DECOMPILE_AND_PATCH BEGIN
    			REPLACE_TEXTUALLY "%oldtext%" "%newtext%"
    		END
    	BUT_ONLY
    END
END 

DEFINE_PATCH_FUNCTION ADD_CONTAINER_ITEM
	INT_VAR charges1=0 charges2=0 charges3=0
	STR_VAR item_to_add="" flags="NONE" container=""
BEGIN
	PATCH_IF ((~%SOURCE_EXT%~ STRING_EQUAL_CASE ~ARE~) AND (item_to_add STRING_COMPARE "") AND (container STRING_COMPARE "")) THEN BEGIN
		SET container_to_add_to = 0
		READ_LONG 0x70 cont_off
 		READ_SHORT 0x74 cont_num
 		FOR (x = 0; x <%cont_num%; x += 1) BEGIN
  			READ_ASCII (%cont_off% + (%x% * 0xc0) + 0x00) ~cont_name~ (32) NULL
  			PATCH_IF (~%cont_name%~ STRING_EQUAL ~%container%~) BEGIN
   				SET container_to_add_to = %x% + 1
   				SET x = (%cont_num% + 1)
  			END
 		END
 		PATCH_IF (container_to_add_to > 0) THEN BEGIN
 			LPM ADD_AREA_ITEM
 		END
	END
END
